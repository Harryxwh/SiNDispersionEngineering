switchtolayout;
deleteall;

nm          =   1e-9;
um          =   1e-6;
mm          =   1e-3;

Lc_inner        =   0;              #length of the straight region
Lc_outer        =   0;              #length of the straight region

width_inner     =   8     *um;    #width of the inner ring waveguide
width_outer     =   2.589 *um;    #width of the outer ring waveguide
gap_ring        =   4     *um;

radius_inner    =   1000    *um;    #radius of the inner ring
radius_outer    =   radius_inner + gap_ring + (width_inner+width_outer)/2; #radius of the outer ring

gap_bus     =   1     *um;    #gap between ring and bus waveguide
width_bus   =   2.8     *um;    #width of the bus waveguide

material_SiN    =   "Si3N4 (Silicon Nitride) - Luke";
material_SiO2    =   "SiO2 (Glass) - Palik";
height          =   100     *nm;    #height of the waveguides

Lx_bus          =   10     *mm;    #length of the bus waveguide
Lx_Buried_Oxide =   10     *mm;    #length of the substrate
Ly_Buried_Oxide =   10     *mm;    #width of the substrate
Lz_Buried_Oxide =   15     *um;    #thickness of the substrate

Lx_cladding =   10     *mm;    #length of the substrate
Ly_cladding =   10     *mm;    #width of the substrate
Lz_cladding =   2      *um;     #thickness of the substrate

Lx_FDE      =   2.5     *mm;
Ly_FDE      =   2.5     *mm;    #width of the substrate
Lz_FDE      =   2     *um;     #thickness of the substrate


#Bus waveguides
addrect;
set("x",0);
set("x span",Lx_bus);
set("y", radius_outer + gap_bus + width_outer/2 + width_bus/2);
set("y span",width_bus);

copy;
set("y", -radius_outer - gap_bus - width_outer/2 - width_bus/2);

# Straight sections for racetrack resonator inner
addrect;
set("x",-radius_inner);
set("x span",width_inner);
set("y", 0);
set("y span",Lc_inner);

copy;
set("x",radius_inner);

#Upper half ring of the racetrack resonator inner
addring;
set("theta start",0);
set("theta stop",180);
set("inner radius",radius_inner-width_inner/2);
set("outer radius",radius_inner+width_inner/2);
set("y", Lc_inner/2);

#Lower half ring of the racetrack resonator inner
copy;
set("theta start",180);
set("theta stop",360);
set("y", - Lc_inner/2);

# Straight sections for racetrack resonator outer
addrect;
set("x",-radius_outer);
set("x span",width_outer);
set("y", 0);
set("y span",Lc_outer);

copy;
set("x",radius_outer);

#Upper half ring of the racetrack resonator outer
addring;
set("theta start",0);
set("theta stop",180);
set("inner radius",radius_outer-width_outer/2);
set("outer radius",radius_outer+width_outer/2);
set("y", Lc_outer/2);

#Lower half ring of the racetrack resonator outer
copy;
set("theta start",180);
set("theta stop",360);
set("y", -Lc_outer/2);

selectall;
set("material",material_SiN);

set("z min",0);
set("z max",height);

#substrate
addrect;
set("name","substrate");
set("x",0);
set("x span",Lx_Buried_Oxide);
set("y",0);
set("y span",Ly_Buried_Oxide);
set("z max",0);
set("z min",-Lz_Buried_Oxide);
set("material",material_SiO2);
set("material",material_SiO2);
set("override mesh order from material database",1);
set("mesh order",3);

#cladding
addrect;
set("name","cladding");
set("x",0);
set("x span",Lx_cladding);
set("y",0);
set("y span",Ly_cladding);
set("z max",Lz_cladding);
set("z min",0);
set("material",material_SiO2);
set("override mesh order from material database",1);
set("mesh order",3);

#Simulation Region
addfdtd;
set("x",0);
set("x span",Lx_FDE);
set("y",0);
set("y span",Ly_FDE);
set("z",0);
set("z span",Lz_FDE);
set("mesh accuracy",2);
set("simulation time",1000e-15);  # 1000 fs

addport;
set("name","input");
set("x",-1.1 * radius_inner);
set("y",radius_outer + gap_bus + width_outer/2 + width_bus/2);
set("z",height/2);
set("y span",width_bus*10);
set("z span",height*10);
set("injection axis","x");
set("direction","forward");


copy;
set("name","through");
set("x",1.1 * radius_inner);
set("direction","backward");

copy;
set("name","drop");
set("x",-1.1 * radius_inner);
set("y",-radius_outer - gap_bus - width_outer/2 - width_bus/2);
set("direction","forward");

# updateportmodes(1);

select("FDTD::ports");
set("source port","input");
set("source mode","fundamental TE mode");

addprofile;
set("name","full_profile");
set("monitor type",7);  # 2D z-normal
set("x",0);
set("x span",1.3 * Lx_FDE);
set("y",0);
set("y span",1.3 * Ly_FDE);
set("z",height/2);

addtime;
set("name","through");
set("monitor type",1);  # point
set("x",1.1 * radius_inner);
set("y",radius_outer + gap_bus + width_outer/2 + width_bus/2);
set("z",height/2);

#run;

Tdrop_dataset = getresult("FDTD::ports::through","T");
Tdrop_3DFDTD = abs(Tdrop_dataset.T);
lambda_3DFDTD = Tdrop_dataset.lambda;
savedata("2D_concentric_racetrack.ldf",Tdrop_3DFDTD,lambda_3DFDTD);

