##############################################
# setup_RDL
# Description: This function is used to add a taper structure in the lumerical software

# parameters:
# structure_group_name      : name of the RDL group
# len_taper                 : length of the adiabatic taper region
# len_prop                  : length of the straight propagtion region
# width_in                  : width of the narrow side of the taper
# width_prop                : width of the wide side of the taper
# res                       : num of pts when drawing the linear taper
# Lz                        : thickness of the RDL WG
# z                         : z coordinate of the RDL WG
# x_min                     : starting point of the RDL region
# x_taper_offset            : offset of the starting point of the taper in +x direction
# material                  : material of the RDL WG

# Author: Weihao Xu
# Date: May. 5th, 2025
# Email: harryxwh@gmail.com
##############################################

addstructuregroup;
set("name",structure_group_name);
set("x",0);
set("y",0);
set("z",0);
x_max = x_min + 2*len_taper + len_prop + 2*x_taper_offset;

# Taper
xspan = linspace(0,len_taper,res);
slope = (width_in - width_prop)/len_taper;
yspan = slope * (len_taper - xspan) + width_prop;

V = matrix(2*res,2);
#[x,y] points
V(1:2*res,1) = [xspan , flip(xspan,1)];
V(1:2*res,2) = [-yspan/2 , flip(yspan,1)/2];

addpoly;
set("x",x_min + x_taper_offset);
set("y",0);
set("z",z);
set("z span",Lz);
set("vertices",V);
set("material",material);
addtogroup(structure_group_name);

# waveguide prop
addrect;
set("name","waveguide prop");
set("x min",x_min + x_taper_offset + len_taper);
set("x max",x_min + x_taper_offset + len_taper + len_prop);
set("y",0);
set("y span",width_prop);
set("z",z);
set("z span",Lz);
set("material",material);
addtogroup(structure_group_name);

# Taper right
yspan = -slope * (len_taper - xspan) + width_in;

V = matrix(2*res,2);
#[x,y] points
V(1:2*res,1) = [xspan , flip(xspan,1)];
V(1:2*res,2) = [-yspan/2 , flip(yspan,1)/2];

addpoly;
set("x",x_max-len_taper-x_taper_offset);
set("y",0);
set("z",z);
set("z span",Lz);
set("vertices",V);
set("material",material);
addtogroup(structure_group_name);

