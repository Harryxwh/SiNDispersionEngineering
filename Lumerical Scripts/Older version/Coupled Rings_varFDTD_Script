deleteall;
switchtolayout;
nm          =   1e-9;
um          =   1e-6;
Lc_out      =   0;              #length of the straight region
Lc_in       =   0;
gap         =   0.1     *um;    #gap between ring and bus waveguide
width_bus   =   630     *nm;    #width of the inner ring waveguide
radius_out  =   100     *um;
width_out   =   630     *nm;    #width of the outer ring waveguide
radius_in   =   99.11   *um;
width_in    =   550     *nm;    #width of the inner ring waveguide

index       =   3.5     *um;
material_Si =   "Si (Silicon) - Palik";
material_BOX=   "SiO2 (Glass) - Palik";
height      =   350     *nm;    #height of the waveguides

Lx_bus      =   500     *um;    #length of the bus waveguide
Lx_sub      =   400     *um;    #length of the substrate
Ly_sub      =   300     *um;    #width of the substrate


#Bus waveguides
addrect;
set("x",0);
set("x span",Lx_bus);
set("y span",width_bus);
set("y",radius_out+gap+width_out/2+width_bus/2);
copy(0,-2*(radius_out+gap+width_out/2+width_bus/2),0);

# Straight sections for racetrack resonators
copy;
set("y",radius_out);
set("x span",Lc_out);

copy;
set("y",-radius_out);

#Right half of the outer ring
addring;
set("theta start",-90);
set("theta stop",90);
set("inner radius",radius_out-width_out/2);
set("outer radius",radius_out+width_out/2);
set("x",Lc_out/2);

#Left half of the outer ring
copy;
set("theta start",90);
set("theta stop",270);
set("x",-Lc_out/2);

#Right half of the inner ring
addring;
set("theta start",-90);
set("theta stop",90);
set("inner radius",radius_in-width_in/2);
set("outer radius",radius_in+width_in/2);
set("x",Lc_in/2);

copy;
set("theta start",90);
set("theta stop",270);
set("x",-Lc_in/2);

selectall;
set("material",material_Si);
if(material=="<Object defined dielectric>") {
set("index",index);
}
set("z min",0);
set("z max",height);

#substrate
addrect;
set("x",0);
set("x span",Lx_sub);
set("y",0);
set("y span",Ly_sub);
set("z max",0);
set("z min",-4*um);
set("material",material_BOX);

#Simulation Region
addvarfdtd;
set("x",0);
set("x span",1.2*2*radius_out);
set("y",0);
set("y span",1.2*2*radius_out);
set("z",0);
set("z span",4*height);
set("simulation time",5000e-15);  # 5000 fs
#Slab mode position
set("x0",-radius_out);
set("y0",radius_out + gap + 0.5*width_out + 0.5*width_bus);
#Test Points
V = [-0.707*(radius_out-0.5*width_out),0.707*(radius_out-0.5*width_out);
    0.707*(radius_out-0.5*width_out),0.707*(radius_out-0.5*width_out);
    -0.707*(radius_out-0.5*width_out),-0.707*(radius_out-0.5*width_out);
    0.707*(radius_out-0.5*width_out),-0.707*(radius_out-0.5*width_out)];
set("test points",V);
set("mesh accuracy",1);

#Mode source
addmodesource;
set("x",-radius_out);
set("y",radius_out+gap+width_out/2+width_bus/2);
set("injection axis","x-axis");
set("direction","forward");
set("center wavelength",1550*nm);
set("wavelength span",100*nm);

addprofile;
set("name","full_profile");
set("monitor type",7);  # 2D z-normal
set("x",0);
set("x span",1.3*2*radius_out);
set("y",0);
set("y span",1.3*2*radius_out);
set("z",0);

addtime;
set("name","through");
set("monitor type",1);  # point
set("x",Lx_bus*0.8);
set("y",radius_out+gap+width_out/2+width_bus/2);
set("z",0.5*height);

#run;

