deleteall;
switchtolayout;
nm          =   1e-9;
um          =   1e-6;
Lc_out      =   0;              #length of the straight region
Lc_in       =   0;
gap         =   0.1     *um;    #gap between ring and bus waveguide
width_bus   =   630     *nm;    #width of the inner ring waveguide
radius_out  =   100     *um;
width_out   =   630     *nm;    #width of the outer ring waveguide
radius_in   =   99.11   *um;
width_in    =   550     *nm;    #width of the inner ring waveguide

index       =   3.5     *um;
material_Si =   "Si (Silicon) - Palik";
material_BOX=   "SiO2 (Glass) - Palik";
height      =   350     *nm;    #height of the waveguides

Lx_bus      =   500     *um;    #length of the bus waveguide
Lx_sub      =   400     *um;    #length of the substrate
Ly_sub      =   300     *um;    #width of the substrate


#Bus waveguides
addrect;
set("x",0);
set("x span",Lx_bus);
set("y span",width_bus);
set("y",radius_out+gap+width_out/2+width_bus/2);
#copy(0,-2*(radius_out+gap+width_out/2+width_bus/2),0);

# Straight sections for racetrack resonators
addrect;
set("x",-radius_out);
set("x span",width_out);
set("y",0);
set("y span",Lc_out);

copy;
set("x",radius_out);

#Upper half of the outer ring
addring;
set("theta start",0);
set("theta stop",180);
set("inner radius",radius_out-width_out/2);
set("outer radius",radius_out+width_out/2);
set("y",Lc_out/2);

#Lower half of the outer ring
copy;
set("theta start",180);
set("theta stop",360);
set("y",-Lc_out/2);

# Straight sections for racetrack resonators
addrect;
set("x",-radius_in);
set("x span",width_in);
set("y",0);
set("y span",Lc_in);

copy;
set("x",radius_in);

#Upper half of the inner ring
addring;
set("theta start",0);
set("theta stop",180);
set("inner radius",radius_in-width_in/2);
set("outer radius",radius_in+width_in/2);
set("y",Lc_in/2);

copy;
set("theta start",180);
set("theta stop",360);
set("y",-Lc_in/2);
、·
selectall;
set("material",material_Si);
set("z min",0);
set("z max",height);

#substrate
addrect;
set("x",0);
set("x span",Lx_sub);
set("y",0);
set("y span",Ly_sub);
set("z max",0);
set("z min",-4*um);
set("material",material_BOX);

#Simulation Region
addfde;
set("solver type",1);   #2D X normal
set("x",0);
set("y",-radius_out-Lc_out*0.5);
set("y span",2*(width_bus + width_out + width_in));
set("z",0);
set("z span",4*height);
set("y min bc","PML");
set("y max bc","PML");
set("z min bc","PML");
set("z max bc","PML");
set("mesh cells y",50);
set("mesh cells z",50);
set("wavelength",1.55*um);
set("wavelength span",0.1*um);

findmodes;

selectmode(1);
setanalysis("track selected mode",1);
setanalysis("detailed dispersion calculation",1);

frequencysweep;
D=getdata("frequencysweep","D");
f=getdata("frequencysweep","f_D");

plot(c/f*1e6,D*1e6,"Wavelength (um)", "Dispersion (ps/nm/km)");