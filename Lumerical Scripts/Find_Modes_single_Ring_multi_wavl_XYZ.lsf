switchtolayout;
deleteall;

um = 1e-6;
nm = 1e-9;
Lx_WG = 8*um; #width of the 
#Lx_WG = 8*um; #width of the WG
Ly_WG = 0.1*um; #height of the WG
Lz_WG = 50*um; #length of the WG

Lx_Cladding = 50*um; #width of the WG
Ly_Cladding = 2*um; #height of the WG
Lz_Cladding = 50*um; #length of the WG

Lx_Buried_Oxide = 50*um;
Ly_Buried_Oxide = 8*um;
Lz_Buried_Oxide = 50*um;

Lx_FDE = 20*um;
Ly_FDE = 10*um;

mesh_step = 200e-9;
mesh_cells_x = 200;
mesh_cells_y = 100;

Lx_inner = 8*um;
bend_radius_inner = 1000*um;
gap = 5*um;

folder_name = "RingXYZ" + "\\";

#bend_radius =  bend_radius_inner + Lx_inner*0.5 + gap + Lx_WG*0.5 ;
bend_radius =  bend_radius_inner;
?"bend_radius = "+num2str(bend_radius*1e6)+" um";

material_core = "Si3N4 (Silicon Nitride) - Luke";
material_cladding = "SiO2 (Glass) - Palik";

Max_Mode_Idx = 1;

# Define wavelength of simulation
wavelength_min = 1500*nm;
wavelength_max = 1600*nm;
num_sweep_pt = 21;
wavelength_array = linspace(wavelength_min,wavelength_max,num_sweep_pt);

# Create folders if they do not yet exist
#for(wavl_idx=1:num_sweep_pt){
    #format short;
    #wavelength = wavelength_array(wavl_idx);
    
    #folder_path = folder_name + num2str(wavelength*1e9);
    #?"mkdir "+folder_path;
    #system("mkdir "+folder_path);
#}

#WG
addrect;
set("name","WG");
set("x",0);
set("x span",Lx_WG);
set("y min",0);
set("y max",Ly_WG);
#set("z",0);
#set("z span",Lz_WG);
set("override mesh order from material database",true);
set("mesh order",2);
set("material",material_core);


#BOX
addrect;
set("name","BOX");
set("x",0);
set("x span",Lx_Buried_Oxide);
set("y max",0);
set("y min",-Ly_Buried_Oxide);
#set("z",0);
#set("z span",Lz_Buried_Oxide);
set("override mesh order from material database",true);
set("mesh order",3);
set("material",material_cladding);

#Cladding
addrect;
set("name","Cladding");
set("x",0);
set("x span",Lx_Cladding);
set("y min",0);
set("y max",Ly_Cladding);
#set("z",0);
#set("z span",Lz_Cladding);
set("override mesh order from material database",true);
set("mesh order",3);
set("material",material_cladding);


addfde;
set("solver type","2D Z normal");
set("x",0*um);
set("y",0);
set("x span",Lx_FDE);
set("y min",-Ly_FDE/2);
set("y max",Ly_FDE/2);
set("x min bc","Metal");
set("x max bc","Metal");
set("y min bc","Metal");
set("y max bc","Metal");
set("define x mesh by","number of mesh cells");
set("define y mesh by","number of mesh cells");
set("mesh cells x",mesh_cells_x);
set("mesh cells y",mesh_cells_y);

set("bent waveguide",1);
set("bend location x", 0);
set("bend location y", 0);
set("bend location z", 0);
set("bend radius",bend_radius);

# Create arrays to store results
neff_array = zeros(num_sweep_pt);
Comp_name_list = {"Ex","Ey","Ez","Hx","Hy","Hz"};


for(wavl_idx=1:num_sweep_pt){

    switchtolayout;
    wavelength = wavelength_array(wavl_idx);
    
    setnamed("FDE","wavelength",wavelength);

    findmodes;

    format short;
    
    folder_path = folder_name + num2str(wavelength*1e9);
    info_filename = folder_path+ "\Mode_info.txt";
    if(fileexists(info_filename)){rm(info_filename);}
    
    #print header
    write(info_filename,"modeidx,neff,ng,loss,polarization,beta_ang");

    for (mode_idx=1:Max_Mode_Idx){

        neff =pinch(getdata("FDE::data::mode"+num2str(mode_idx),"neff"));
        ng =pinch(getdata("FDE::data::mode"+num2str(mode_idx),"ng"));
        loss =pinch(getdata("FDE::data::mode"+num2str(mode_idx),"loss"));
        polarization=pinch(getdata("FDE::data::mode"+num2str(mode_idx),"TE polarization fraction"));
        
        beta_ang = real(2*pi/wavelength * bend_radius * neff);

        Ex = pinch(getdata("FDE::data::mode"+num2str(mode_idx),"Ex"));
        Ey = pinch(getdata("FDE::data::mode"+num2str(mode_idx),"Ey"));
        Ez = pinch(getdata("FDE::data::mode"+num2str(mode_idx),"Ez"));
        Hx = pinch(getdata("FDE::data::mode"+num2str(mode_idx),"Hx"));
        Hy = pinch(getdata("FDE::data::mode"+num2str(mode_idx),"Hy"));
        Hz = pinch(getdata("FDE::data::mode"+num2str(mode_idx),"Hz"));

        format long;
        Comp_list = {Ex,Ey,Ez,Hx,Hy,Hz};
        info = num2str(mode_idx) +","+ num2str(neff) +","+ num2str(ng) +","+ num2str(loss) +","+ num2str(polarization) +","+ num2str(beta_ang);
        write(info_filename,info);

        #Fundamental mode
        if(mode_idx == 1){
            neff_array(mode_idx) = real(neff);
            ?"Wavelength: " + num2str(wavelength*1e6) + " um";
            ?"Effective index: " + num2str(real(neff));
            ?"beta_ang: " + num2str(beta_ang);
        }
        #write mode profile matrix
        for (j=1:6){
            format short;
            filename = folder_path+"\Mode"+num2str(mode_idx)+"_"+Comp_name_list{j}+".txt";
            if(fileexists(filename)){
                rm(filename);
            }
        format long; # set num2str() to return 16 digits of precision
        write(filename,num2str(Comp_list{j}));
        }

    }
}
